#!/bin/tcsh

##
## USAGE: pipeline-new-analysis <pipeline name> /path/to/analysis_dir [SYMLINK-CODE=false]
## pipeline name should be either:
## hicseq-standard
## or:
## chipseq-standard
## EXAMPLE: 
## pipeline-new-analysis hicseq-standard /home/my_new_analysis
##

if ($#argv < 2) then
  grep '^##' $0
  exit
endif

set pipeline_name = $1
set analysis_dir = $2
set symlink = $3

mkdir -p $analysis_dir

# original code repo
set repo_code_dir = `readlink -f $0 | sed 's/\/[^/]\+$//'`/..

# copy pipeline structure to new analysis directory
echo "Copying pipeline structure and code into $analysis_dir..."
set pipeline_dir = $repo_code_dir/../pipelines/$pipeline_name
set pipeline_dir = `echo $pipeline_dir | sed 's/[/]\+$//'`    # remove trailing '/'
rsync -auv --exclude=results $pipeline_dir/ $analysis_dir

# go to analysis dir to create various symlinks
cd $analysis_dir

# setup link to original code repo
ln -s $repo_code_dir code.origin

# create a copy OR symbolic link to the code
rm -f code
if ($symlink == 'true') then
  ln -s $repo_code_dir/code.$pipeline_name code
else
  rsync -auv --exclude=src $repo_code_dir/ code.repo
  ln -s code.repo/code.$pipeline_name code
endif

# create symbolic link to data repository (genomes, etc)
rm -f data
ln -s $pipeline_dir/data

# create fastq dir
mkdir inputs/fastq

# write project_info.txt file

set author = `whoami | xargs finger | sed -E 's/\t+/\n/g' | grep "Name:\|Office:" | sed -E 's/ +/ /g' | cut -d ' ' -f2 | xargs -n2 | awk '{print $2"."$1}'`
set author_email = `echo $author"@nyumc.org" | sed 's/ /./g'`
set abs_analysis_dir = `readlink -f $analysis_dir | xargs dirname`
set PI = `echo $abs_analysis_dir | tr '\/' '\n' | grep -E 'Lab$' | sed -E 's/Lab$//'`
set person = `echo $abs_analysis_dir | sed -E 's/Lab\//\n/g' | tail -1 | rev | xargs basename | rev`
set code_version = `cat $repo_code_dir/../.git/logs/HEAD | tail -1 | cut -d' ' -f2 | head -c 7`
if ($code_version == "") then
  set code_version = `cat $repo_code_dir/../../project_info.txt | tail -1 | cut -d' ' -f2`":"`dirname $0`
endif

cat << EOF >! project_info.txt
PROJECT-DIR: $abs_analysis_dir

PROJECT-ID: ${analysis_dir}~${person}~${pipeline_name}

PROJECT-ID-SHORT: ${analysis_dir}~${person}

REPORT-AUTHOR: $author

REPORT-AUTHOR-EMAIL: $author_email

PI-NAME: Dr. $PI

CODE-VERSION: $code_version
EOF
