#!/bin/tcsh

##
## USAGE: pipeline-new-analysis <pipeline name> PATH-TO-NEW-ANALYSIS
##
## EXAMPLE: pipeline-new-analysis2 hicseq-standard /home/my_new_analysis
##

if ($#argv != 2) then
  grep '^##' $0
  exit
endif

set pipeline_name = $1
set analysis_path = $2

# shallow clone
set repo_path = `readlink -f $0 | sed 's/\/[^/]\+$//'`/../..
git clone --depth 1 $repo_path/ $analysis_path/

# build code
cd $analysis_path/code/src
make

# create various symlinks
cd ../..
ln -s pipelines/$pipeline_name analysis
#ln -s $repo_path/data

# create fastq dir
mkdir analysis/inputs/fastq

# write project_info.txt file
cd analysis/
set author = `whoami | xargs finger | sed -E 's/\t+/\n/g' | grep "Name:\|Office:" | sed -E 's/ +/ /g' | cut -d ' ' -f2 | xargs -n2 | awk '{print $2"."$1}'`
set author_email = `echo $author"@nyumc.org" | sed 's/ /./g'`
set abs_analysis_dir = `readlink -f $analysis_path | xargs dirname`
set PI = `echo $abs_analysis_dir | tr '\/' '\n' | grep -E 'Lab$' | sed -E 's/Lab$//'`
set person = `echo $abs_analysis_dir | sed -E 's/Lab\//\n/g' | tail -1 | rev | xargs basename | rev`
set code_version = `cat $repo_path/.git/logs/HEAD | tail -1 | cut -d' ' -f2 | head -c 7`
if ($code_version == "") then
  set code_version = `cat $repo_path/pipeline/$pipeline_name/project_info.txt | tail -1 | cut -d' ' -f2`":"`dirname $0`
endif

cat << EOF >! project_info.txt
PROJECT-DIR: $abs_analysis_dir

PROJECT-ID: ${analysis_path}~${person}~${pipeline_name}

PROJECT-ID-SHORT: ${analysis_path}~${person}

REPORT-AUTHOR: $author

REPORT-AUTHOR-EMAIL: $author_email

PI-NAME: Dr. $PI

CODE-VERSION: $code_version
